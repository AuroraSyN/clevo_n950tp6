# Maintainer: Pekka Helenius <fincer89 [at] hotmail [dot] com>
# Contributor: Arnoud Willemsen <mail@lynthium.com>

pkgname='clevo-xsm-wmi-util'
pkgver='1.1'
_pkgtag='6bfe9fe44e04'
pkgrel=1
pkgdesc='A GUI control utility for the clevo-xsm-wmi driver.'
arch=('i686' 'x86_64')
license=('GPL')
url='https://bitbucket.org/tuxedocomputers/clevo-xsm-wmi'
source=(
    "clevo-xsm-wmi-${pkgver}.src.tar.gz::https://bitbucket.org/tuxedocomputers/clevo-xsm-wmi/get/v${pkgver}.tar.gz"
    "clevo-xsm-wmi-util.desktop"
    "patch_n950tp6-support.patch"
    "patch_add-orange.patch"
)
options=(!emptydirs)
depends=('clevo-xsm-wmi' 'qt5-base')

prepare() {
    cd "${srcdir}/tuxedocomputers-clevo-xsm-wmi-${_pkgtag}/"
    patch -Np1 -i ../patch_n950tp6-support.patch
    patch -Np1 -i ../patch_add-orange.patch
}

build() {
    cd "${srcdir}/tuxedocomputers-clevo-xsm-wmi-${_pkgtag}/utility"
    qmake && make
}
package() {
    cd "${srcdir}/tuxedocomputers-clevo-xsm-wmi-${_pkgtag}/utility"

    mkdir -p ${pkgdir}/usr/share/applications/
    install -m644 ${srcdir}/clevo-xsm-wmi-util.desktop ${pkgdir}/usr/share/applications/clevo-xsm-wmi-util.desktop
    install -Dm755 clevo-xsm-wmi ${pkgdir}/usr/bin/clevo-xsm-wmi
    install -Dm644 systemd/clevo-xsm-wmi.service ${pkgdir}/usr/lib/systemd/system/clevo-xsm-wmi.service
    install -D -m644 /usr/share/licenses/common/GPL2/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
sha256sums=('630ff6dbfa90c8a9285badf75b60579b2a9495e85862b62ef77a3654535e4b72'
            '5a7718c3ec49069a93d7bef5d9cfd2ca302eb612f1a8f2e05f3e0aec15047019'
            '5cd43e09f262db1732422c874774869114362586febe00bb6341a5e8932059c9'
            '4e2833df06f88efae08043fee711c9bba80b769a7f51f860a729afaab0d0fa51')
